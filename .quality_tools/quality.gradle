apply plugin: 'checkstyle'
apply plugin: 'pmd'

ext {
  versions = [
    checkstyle: '8.18',
    pmd: '6.12.0'
  ]
}

def qualityConfigDir = "$project.rootDir/.support"
def reportsDir = "$project.rootDir/__reports__"

check.dependsOn 'checkstyle', 'pmd'

// ------
// Checkstyle

checkstyle {
  configDir = file("$qualityConfigDir/checkstyle")
  configFile = file("$qualityConfigDir/checkstyle/checkstyle.xml")
  maxWarnings = 0
  ignoreFailures = false
  toolVersion versions.checkstyle
}

task checkstyle {
  group 'Quality Assurance'
  description 'Run Checkstyle'

  dependsOn 'checkstyleMain'
  dependsOn 'checkstyleTest'
}


// ------
// PMD

pmd {
  ruleSets = []
  ruleSetFiles = files("$qualityConfigDir/pmd/pmd-ruleset.xml")
  ignoreFailures = false
  toolVersion = versions.pmd
}

task pmd {
  group 'Quality Assurance'
  description 'Run PMD'

  dependsOn 'pmdMain'
  dependsOn 'pmdTest'
}
